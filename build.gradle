// To build the Sponge plugin, run 'gradle sponge'
// To build the Bukkit plugin with support for all versions of CraftBukkit since 1.4.7, run 'gradle bukkit'
// To build the Bukkit plugin with support for only the latest version of CraftBukkit, run 'gradle bukkitLatest'

plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

group = 'com.github.hoqhuuep'
version = '1.0.8'

allprojects {
    repositories {
        jcenter()
        maven {
            name = 'sponge-repo'
            url = 'http://repo.spongepowered.org/maven'
        }
        maven {
            name = 'bukkit-repo'
            url 'http://repo.bukkit.org/content/groups/public/'
        }
        maven {
            name = 'spigot-repo'
            url 'https://hub.spigotmc.org/nexus/content/groups/public/'
        }
        //maven {
        //    name = 'mcstats-repo'
        //    url 'http://repo.mcstats.org/content/repositories/public'
        //}
        mavenLocal()
    }

    apply plugin: 'eclipse'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

project(':IslandCraft-API') {
}

project(':IslandCraft-Common') {
    dependencies {
        compile project(':IslandCraft-API')
    }
}

project(':IslandCraft-Sponge') {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    dependencies {
        compile project(':IslandCraft-Common')
        compile group: 'org.spongepowered', name:'spongeapi', version: '3.0.0'
        //compile group: 'org.mcstats.sponge', name: 'metrics', version: 'R8-SNAPSHOT'
    }
}

project(':IslandCraft-Bukkit') {
    dependencies {
        compile project(':IslandCraft-NMS')
        compile project(':IslandCraft-Common')
        compile group: 'org.bukkit', name: 'bukkit', version: '1.12-R0.1-SNAPSHOT'
        //compile group: 'org.mcstats.bukkit', name: 'metrics', version: 'R7'
        compile group: 'org.avaje', name: 'ebean', version: '2.8.1'
    }
}

project(':IslandCraft-NMS') {
    dependencies {
        compile project(':IslandCraft-API')
        compile group: 'org.bukkit', name: 'bukkit', version: '1.12-R0.1-SNAPSHOT'
    }
}

project(':IslandCraft-NMS-v1_12_R1') {
    dependencies {
        compile project(':IslandCraft-NMS')
        compile group: 'org.bukkit', name: 'craftbukkit', version: '1.12-R0.1-SNAPSHOT'
    }
}

project(':IslandCraft-NMS-v1_13_R1') {
    dependencies {
        compile project(':IslandCraft-NMS')
        compile group: 'org.bukkit', name: 'craftbukkit', version: '1.13-R0.1-SNAPSHOT'
    }
}

configurations {
    spongePlugin
    bukkitPlugin
    bukkitLatest
}

dependencies {
    spongePlugin (project(':IslandCraft-Sponge')) {
        exclude group: 'org.spongepowered'
    }
}

task sponge(type: com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    description = 'Create the IslandCraft Sponge plugin JAR'
    group = project.group
    baseName = project.name
    appendix = 'Sponge'
    version = project.version
    configurations = [ project.configurations.spongePlugin ]
    from {
        project.configurations.spongePlugin
    }
    relocate 'org.mcstats', 'com.github.hoqhuuep.islandcraft.mcstats'
}

dependencies {
    bukkitPlugin(project(':IslandCraft-Bukkit')) {
        exclude group: 'org.bukkit'
    }
    bukkitPlugin(project(':IslandCraft-NMS-v1_12_R1')) {
        exclude group: 'org.bukkit'
    }
    bukkitPlugin(project(':IslandCraft-NMS-v1_13_R1')) {
        exclude group: 'org.bukkit'
    }
}

task bukkit(type: com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    description = 'Create the IslandCraft Bukkit plugin JAR'
    group = project.group
    baseName = project.name
    appendix = 'Bukkit'
    version = project.version
    configurations = [ project.configurations.bukkitPlugin ]
    from {
        project.configurations.bukkitPlugin
    }
    relocate 'org.mcstats', 'com.github.hoqhuuep.islandcraft.mcstats'
}

dependencies {
    bukkitLatest (project(':IslandCraft-Bukkit')) {
        exclude group: 'org.bukkit'
    }
    bukkitLatest(project(':IslandCraft-NMS-v1_13_R1')) {
        exclude group: 'org.bukkit'
    }
}

task bukkitLatest(type: com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    description = 'Create the IslandCraft Bukkit plugin JAR'
    group = project.group
    baseName = project.name
    appendix = 'BukkitLatest'
    version = project.version
    configurations = [ project.configurations.bukkitLatest ]
    from {
        project.configurations.bukkitLatest
    }
    relocate 'org.mcstats', 'com.github.hoqhuuep.islandcraft.mcstats'
}
